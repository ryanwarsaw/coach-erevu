apply plugin: 'com.android.application'

// Process key store properties from keystore.properties file.
def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
  compileSdkVersion 28

  defaultConfig {
    applicationId "com.ryanwarsaw.coach_erevu"
    minSdkVersion 19
    targetSdkVersion 28
    versionCode 5
    versionName "3.1"
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }

  signingConfigs {
    publish {
      keyAlias keystoreProperties['keyAlias']
      keyPassword keystoreProperties['keyPassword']
      storeFile file(keystoreProperties['storeFile'])
      storePassword keystoreProperties['storePassword']
    }
  }

  buildTypes {
    latest {
      signingConfig signingConfigs.publish
    }

    bundle {
      signingConfig signingConfigs.publish
    }
  }

  android.variantFilter { variant ->
    if (variant.buildType.name == 'release' || variant.buildType.name == 'debug') {
      variant.setIgnore(true)
    }
  }

  applicationVariants.all { variant ->
    variant.outputs.all {
      outputFileName = "coach-erevu-v${android.defaultConfig.versionName}-${variant.buildType.name}.apk"
    }

    // Tell Android to not bundle the videos when building "latest" releases.
    if (variant.buildType.name == 'latest') {
      variant.mergeAssets.doLast {
        delete(fileTree(dir: variant.mergeAssets.outputDir, includes: ['**/*.mp4']))
      }
    }
  }

  lintOptions {
    disable 'GoogleAppIndexingWarning'
    baseline file("lint-baseline.xml")
  }
}

dependencies {
  compileOnly 'org.projectlombok:lombok:1.18.2'
  annotationProcessor 'org.projectlombok:lombok:1.18.2'

  implementation fileTree(dir: 'libs', include: ['*.jar'])
  implementation 'com.android.support:support-v4:28.0.0'
  implementation 'com.android.support:appcompat-v7:28.0.0'
  implementation 'com.android.support.constraint:constraint-layout:1.1.3'
  implementation 'com.google.code.gson:gson:2.8.2'
  implementation 'com.google.android.exoplayer:exoplayer-core:2.7.3'
  implementation 'com.google.android.exoplayer:exoplayer-ui:2.7.3'

  testImplementation 'junit:junit:4.12'
  androidTestImplementation 'com.android.support.test:runner:1.0.2'
  androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
